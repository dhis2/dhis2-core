#!/usr/bin/env groovy

@Library('pipeline-library') _

pipeline {
    agent {
        label 'ec2-jdk17'
    }

    options {
        timeout(time: 60)
    }

    environment {
        MAVEN_OPTS = '-Xms1024m -Xmx4096m -Dmaven.wagon.http.retryHandler.class=standard -Dmaven.wagon.http.retryHandler.count=3 -Dmaven.wagon.httpconnectionManager.ttlSeconds=125'
        GITHUB_TOKEN = credentials('github-token')
        DHIS2_VERSION = readMavenPom(file: 'dhis-2/pom.xml').getVersion()
        DOCKER_IMAGE_NAME_DEV = "${DOCKER_HUB_OWNER}/core-dev"
        DOCKER_IMAGE_NAME = "${DOCKER_HUB_OWNER}/core"
        DOCKER_IMAGE_TAG = "${env.DHIS2_VERSION.replace('SNAPSHOT', 'rc')}"
        DOCKER_IMAGE_NAME_PUBLISH_SOURCE = "tomcat:9.0-jre17" // source of image to publish to Dockerhub (one of the matrix axes)
        DOCKER_IMAGE_NAME_PUBLISH_TARGET = "${DOCKER_IMAGE_NAME}:${DOCKER_IMAGE_TAG}" // used to publish to Dockerhub
    }

    stages {
        stage ('Patch') {
            steps {
                echo 'Patching DHIS2 ...'
                sh './run-cve-patcher.sh'
            }
        }

        stage ('Build') {
            steps {
                echo 'Building DHIS2 ...'
                script {
                    withMaven(options: [artifactsPublisher(disabled: true)]) {
                        sh 'mvn -X -T 4 --batch-mode --no-transfer-progress clean install -f dhis-2/pom.xml -P -default --update-snapshots -pl -dhis-web-embedded-jetty,-dhis-test-coverage'
                        sh 'mvn -X -T 4 --batch-mode --no-transfer-progress install -f dhis-2/dhis-web/pom.xml -P -default --update-snapshots'
                    }
                }
            }
        }

        stage('Run api tests') {
            environment {
                RP_UUID = credentials('report-portal-access-uuid')
                RP_ENABLE = 'true'
                RP_ATTRIBUTES = "version:${env.GIT_BRANCH};"
                DOCKER_IMAGE_NAME_BASE = 'tomcat'
            }

            matrix {
                axes {
                    axis {
                        name 'DOCKER_IMAGE_TAG_BASE'
                        values '9.0-jre17', '8.5-jre17'
                    }
                }

                environment {
                    DOCKER_IMAGE_NAME_BASE_FULL = "${DOCKER_IMAGE_NAME_BASE}:${DOCKER_IMAGE_TAG_BASE}"
                    DOCKER_IMAGE_NAME_FULL = "${DOCKER_IMAGE_NAME_DEV}:${DOCKER_IMAGE_TAG}-${DOCKER_IMAGE_TAG_BASE}" // used to test against different Tomcat variants
                }

                stages {
                    stage('Build Docker image') {
                        steps {
                            withDockerRegistry([credentialsId: "docker-hub-credentials", url: ""]) {
                                withMaven(options: [artifactsPublisher(disabled: true)]) {
                                    sh "mvn --batch-mode --no-transfer-progress -DskipTests -Dmaven.test.skip=true \
                                            -f dhis-2/dhis-web/dhis-web-portal/pom.xml jib:build -PjibBuild \
                                            -Djib.from.image=${DOCKER_IMAGE_NAME_BASE_FULL} -Djib.to.image=${DOCKER_IMAGE_NAME_FULL} \
                                            -Djib.container.labels=DHIS2_VERSION=${DHIS2_VERSION},DHIS2_BUILD_REVISION=${GIT_COMMIT},DHIS2_BUILD_BRANCH=${env.GIT_BRANCH}"
                                }
                            }
                        }
                    }

                    stage('Run tests') {
                        steps {
                            script {
                                dir("dhis-2/dhis-test-e2e") {
                                    sh "docker pull ${DOCKER_IMAGE_NAME_FULL}"
                                    sh "DHIS2_IMAGE=${DOCKER_IMAGE_NAME_FULL} docker-compose --project-name ${DOCKER_IMAGE_TAG_BASE} --file docker-compose.yml --file docker-compose.e2e.yml up --remove-orphans --exit-code-from test"
                                }
                            }
                        }

                        post {
                            always {
                                script {
                                    dir("dhis-2/dhis-test-e2e") {
                                        archiveArtifacts artifacts: "coverage.csv", allowEmptyArchive: true
                                    }
                                }
                            }

                            failure {
                                script {
                                    dir("dhis-2/dhis-test-e2e") {
                                        sh "docker-compose --project-name ${DOCKER_IMAGE_TAG_BASE} logs web > ${DOCKER_IMAGE_TAG_BASE}_logs.txt"
                                        archiveArtifacts artifacts: "${DOCKER_IMAGE_TAG_BASE}_logs.txt"
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }

        stage('Publish images') {
            steps {
                withDockerRegistry([credentialsId: "docker-hub-credentials", url: ""]) {
                    withMaven(options: [artifactsPublisher(disabled: true)]) {
                        sh "mvn --batch-mode --no-transfer-progress -DskipTests -Dmaven.test.skip=true \
                                            -f dhis-2/dhis-web/dhis-web-portal/pom.xml jib:build -PjibBuild \
                                            -Djib.from.image=${DOCKER_IMAGE_NAME_PUBLISH_SOURCE} -Djib.to.image=${DOCKER_IMAGE_NAME_PUBLISH_TARGET} \
                                            -Djib.container.labels=DHIS2_VERSION=${DHIS2_VERSION},DHIS2_BUILD_REVISION=${GIT_COMMIT},DHIS2_BUILD_BRANCH=${env.GIT_BRANCH}"
                    }
                }
            }
        }

        stage ('Sync WAR') {
            steps {
                echo 'Syncing WAR ...'
                sh 'curl "https://raw.githubusercontent.com/dhis2/dhis2-server-setup/master/ci/scripts/copy-war-s3.sh" -O'
                sh 'chmod +x copy-war-s3.sh'
                sh './copy-war-s3.sh stable ${GIT_BRANCH}'
            }
        }

        stage('Update IM Play instance') {
            when {
                expression { !env.DOCKER_IMAGE_TAG.endsWith("rc") }
            }

            environment {
                HTTP = "http --check-status"
                IM_REPO_URL = "https://github.com/dhis2-sre/im-manager"
                IM_HOST = "https://api.im.dhis2.org"
                INSTANCE_URL = "https://play.im.dhis2.org"
                IMAGE_REPOSITORY = "core"
                IMAGE_PULL_POLICY = "Always"
                FLYWAY_MIGRATE_OUT_OF_ORDER = "true"
                FLYWAY_REPAIR_BEFORE_MIGRATION = "true"
                INSTANCE_TTL = "315360000"
                STARTUP_PROBE_FAILURE_THRESHOLD = "50"
                LIVENESS_PROBE_TIMEOUT_SECONDS = "3"
                READINESS_PROBE_TIMEOUT_SECONDS = "3"
            }

            steps {
                echo 'Creating DHIS2 instance on IM...'
                script {
                    withCredentials([usernamePassword(credentialsId: 'dhis2-im-bot', passwordVariable: 'PASSWORD', usernameVariable: 'USER_EMAIL')]) {
                        dir('im-manager') {
                            gitHelper.sparseCheckout(IM_REPO_URL, "${gitHelper.getLatestTag(IM_REPO_URL)}", '/scripts')

                            def version = env.GIT_BRANCH
                            if (version.startsWith("patch/")) {
                                version = version.split("/")[1]
                            }
                            env.IMAGE_TAG = version
                            def instanceName = "stable-${version.replace(".", "-")}"

                            // If version contains more than 2 dots... It's a hotfix
                            def isHotfix = version.length() - version.replace(".", "").length() > 2
                            if (isHotfix) {
                                def endIndex = version.lastIndexOf(".")
                                version = version.substring(0, endIndex)
                            }

                            dir('scripts/databases') {
                                env.DATABASE_ID = sh(
                                        returnStdout: true,
                                        script: "./list.sh | jq -r '.[] | select(.name == \"test-dbs\") | .databases[] | select(.name == \"sierra-leone/${version}.sql.gz\") | .slug'"
                                ).trim()
                            }

                            sh '[ -n "$DATABASE_ID" ]'
                            echo "Database: ${env.DATABASE_ID}"

                            dir('scripts/instances') {
                                sh "(./findByName.sh play ${instanceName} && ./restart.sh play ${instanceName}) || ./deploy-dhis2.sh play ${instanceName}"
                                timeout(5) {
                                    waitFor.statusOk("${env.INSTANCE_URL}/${instanceName}")
                                }
                            }
                        }
                    }
                }
            }
        }
    }

    post {
        failure {
            script {
                slack.sendFailureMessage()
            }
        }

        aborted {
            script {
                slack.sendTimedoutMessage()
            }
        }
    }
}
